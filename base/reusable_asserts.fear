package base;

_AssertApprox[T]:DataType[T,T]{
  .eqDelta(n:T, delta:T):Bool;
  .assertEqDelta(expected:T, delta:T):Void -> Assert!(
    this.eqDelta(expected,delta),
    { `Expected within delta`
      | `expected: `+(this.close(expected))
      | `delta: `+(this.close(delta))
      | `actual: `+this });
  .assertEqDelta(expected:T, delta:T, msg:F[Str]):Void -> Assert!(
    this.eqDelta(expected,delta),
    { msg#
      | `expected: `+(this.close(expected))
      | `delta: `+(this.close(delta))
      | `actual: `+this });
  }
  
_AssertSize:{
  read .size: Nat;

  read .assertSize(expected: Nat): Void -> this.assertSize(expected,{``});
  read .assertSize(expected: Nat, msg: F[Str]): Void -> Assert!(
    this.size == expected,
    { msg#
      | `Expected size: `+expected
      | `Actual size: `+(this.size) });
  }

_AssertImmSize:{
  imm .size: Nat;

  imm .assertSize(expected: Nat): Void -> this.assertSize(expected,{``});
  imm .assertSize(expected: Nat, msg: F[Str]): Void -> Assert!(
    this.size == expected,
    { msg#
      | `Expected size: `+expected
      | `Actual size: `+(this.size) });
  }

_AssertEmpty:{
  read .isEmpty: Bool;

  read .assertEmpty: Void -> this.assertEmpty({``});
  read .assertEmpty(msg: F[Str]): Void -> Assert!(
    this.isEmpty,
    { msg# | `Expected empty` });
  }
_AssertImmEmpty:{
  imm .isEmpty: Bool;

  imm .assertEmpty: Void -> this.assertEmpty({``});
  imm .assertEmpty(msg: F[Str]): Void -> Assert!(
    this.isEmpty,
    { msg# | `Expected empty` });
  }